plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id "com.github.spotbugs" version "4.7.2"
    id 'jacoco'
    id 'java'
}

group 'de.codehat'
version '1.0.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.theholywaffle:teamspeak3-api:1.3.0'
    implementation 'org.slf4j:slf4j-nop:1.7.31'
    implementation 'org.tinylog:tinylog-impl:2.3.2'
    implementation 'com.beust:jcommander:1.81'
    implementation 'org.aeonbits.owner:owner:1.0.12'
    implementation 'com.google.inject:guice:5.0.1'
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.7.2')
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

// See: https://stackoverflow.com/a/51918271/9369359
ext.moduleName = 'de.codehat.teamspeak.afkbot'
compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'de.codehat.teamspeak.afkbot.Main'
    }
}

shadowJar {
    archiveClassifier.set('')
    archiveVersion.set('')
}

spotbugsMain {
    reports {
        xml {
            enabled = false
        }
        html {
            enabled = true
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}